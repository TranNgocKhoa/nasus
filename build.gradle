plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.tranngockhoa'
version '0.0.3'

repositories {
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceCompatibility = 17
targetCompatibility = 17

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.hasProperty("osshUserName") ? project.property("osshUserName") : ""
                password = project.hasProperty("osshPassword") ? project.property("osshPassword") : ""
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.github.tranngockhoa'
            version '0.0.3'
            artifactId = 'nasus'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Nasus'
                description = 'Nasus IoC'
                url = 'https://github.com/TranNgocKhoa/nasus.git'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tranngockhoa'
                        name = 'Tran Ngoc Khoa'
                        email = 'tranngoc.khoa.spk@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TranNgocKhoa/nasus.git'
                    developerConnection = 'scm:git:ssh://github.com/TranNgocKhoa/nasus.git'
                    url = 'https://github.com/TranNgocKhoa/nasus.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
}